plugins {
    id "org.gradle.java"
    id "org.gradle.eclipse"
    id "org.gradle.jacoco"
    id "com.scuilion.syntastic" version "0.3.6"
}

group = 'edu.stanford.hivdb'
version = '3.0.0b2'

description = """Sierra Unit Tests"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile project(':sierra-core')
    compile project(':hivfacts-java')
    compile project(':DrugResistance')
    compile project(':WebApplications')

    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.apache.commons:commons-math3:3.6.1'

    implementation 'commons-cli:commons-cli:1.4'

    implementation 'org.apache.logging.log4j:log4j-api:2.12.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
    implementation 'org.slf4j:slf4j-api:1.7.29'

    implementation 'com.google.guava:guava:28.1-jre'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.code.gson:gson-extras:2.8.5'

    implementation 'com.graphql-java:graphql-java:13.0'

    testImplementation 'junit:junit:4.12'

    implementation 'org.apache.httpcomponents:httpclient:4.5.10'
    implementation 'org.apache.httpcomponents:httpcore:4.4.12'

    implementation 'com.amazonaws:aws-java-sdk-lambda:1.11.688'

    testImplementation 'org.mockito:mockito-core:3.2.0'
}

ext {
    jacocoReportClasses = []
    rootProject.subprojects.each { project ->
        if (project.name in ["hivfacts-java", "sierra-core"]) {
            jacocoReportClasses.add(project.sourceSets.main.output)
        }
    }
}

test {
    // exclude '**/*Regression*.class'

    // testLogging {
    //     showStandardStreams = true
	// 	events "failed"
	// 	exceptionFormat "full"
    // }
}

jacocoTestReport {
    classDirectories =  files(jacocoReportClasses)
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport


// Aligner test tasks
task clearCachable(type: Exec, group: 'Generator', description: 'Clean files created by Cachable.') {
    commandLine 'rm'
    args '-rf', 'src/main/resources/__cached_classes'
}

task updateCachable(type: JavaExec, group: 'Generator', description: 'Generate files with Cachable.') {
    main = 'edu.stanford.hivdb.utilities.Cachable'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'hivdb.updateCachable', """
    """
}

updateCachable.dependsOn classes
updateCachable.dependsOn clearCachable
updateCachable.dependsOn ':utilities:updateCachable'