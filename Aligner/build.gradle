plugins {
    id "org.gradle.java"
    id "org.gradle.jacoco"
    id "org.gradle.eclipse"
    id "com.scuilion.syntastic" version "0.3.6"
}

group = 'edu.stanford.hivdb'
version = '2.3.5'

description = """Aligner"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
     mavenCentral()
}

dependencies {
    compile 'org.apache.httpcomponents:httpclient:4.5.6'
    compile 'org.apache.httpcomponents:httpcore:4.4.10'
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'org.apache.commons:commons-math3:3.0'
    compile 'org.apache.logging.log4j:log4j-api:2.5'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'mysql:mysql-connector-java:5.1.38'
    compile 'com.google.guava:guava:21.0'
    compile 'com.mchange:c3p0:0.9.5.2'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.amazonaws:aws-java-sdk-lambda:1.11.407'
    // compile 'software.amazon.awssdk:lambda:2.0.0-preview-11'
    compile(project(':Utilities')) {
        exclude module: 'testutils'
        exclude group: 'junit'
        exclude group: 'org.mockito'
    }
    compile(project(':hiv-genotyper'))
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile project(':Utilities')
    testCompile project(':TestResources')
}

task clearCachable(type: Exec, group: 'Generator', description: 'Clean files created by Cachable.') {
    commandLine 'rm'
    args '-rf', 'src/main/resources/__cached_classes'
}

task updateCachable(type: JavaExec, group: 'Generator', description: 'Generate files with Cachable.') {
    main = 'edu.stanford.hivdb.utilities.Cachable'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'hivdb.updateCachable', """
    """
}

updateCachable.dependsOn classes
updateCachable.dependsOn clearCachable
updateCachable.dependsOn ':Utilities:updateCachable'

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport
