plugins {
    id "org.gradle.java"
    id "org.gradle.jacoco"
    id "org.gradle.eclipse"
    id "com.scuilion.syntastic" version "0.3.6"
}

group = 'edu.stanford.hivdb'
version = '2.2.10'

description = """Utilities"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/hivdb/hivdb'
    }
    maven {
    	url 'https://artifactory.cronapp.io/public-release/'
    }
}

dependencies {
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'org.apache.commons:commons-math3:3.0'
    compile 'commons-codec:commons-codec:1.5'
    compile 'org.apache.logging.log4j:log4j-api:2.5'
    compile 'org.apache.logging.log4j:log4j-core:2.5'
    compile 'mysql:mysql-connector-java:5.1.38'
    compile 'com.google.guava:guava:21.0'
    compile 'com.mchange:c3p0:0.9.5.2'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'com.google.code.gson:gson-extras:2.8.5'
    compile 'commons-io:commons-io:2.4'
    compile 'com.codepoetics:protonpack:1.8'
	compile 'net.sf.jfasta:jfasta:2.1.1'
    compile 'junit:junit:4.12'
    compile 'org.mockito:mockito-core:1.9.5'
    compile 'org.apache.httpcomponents:httpclient:4.5.6'
    compile 'org.apache.httpcomponents:httpcore:4.4.10'
    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile 'edu.stanford.hivdb.aapcnt:hiv-aapcnt-java:2018.11-2'
    compile project(':TestResources')
}

task clearCachable(type: Exec, group: 'Generator', description: 'Clean files created by Cachable.') {
    commandLine 'rm'
    args '-rf', 'src/main/resources/__cached_classes'
}

task updateCachable(type: JavaExec, group: 'Generator', description: 'Generate files with Cachable.') {
    main = 'edu.stanford.hivdb.utilities.Cachable'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'hivdb.updateCachable', """
        edu.stanford.hivdb.mutations.Apobec
        edu.stanford.hivdb.mutations.MutationTypePairs
        edu.stanford.hivdb.mutations.Sdrms
        edu.stanford.hivdb.mutations.DRMs
        edu.stanford.hivdb.mutations.Tsms
        edu.stanford.hivdb.mutations.MutationPrevalences
    """
}

updateCachable.dependsOn classes
updateCachable.dependsOn clearCachable

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['edu/stanford/hivdb/testutils'])
        })
    }
}

check.dependsOn jacocoTestReport
